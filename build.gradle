plugins {
    id 'maven-publish'
    id 'signing'
    id 'idea'
    id "io.spring.dependency-management" version "1.0.8.RELEASE"
}

allprojects {
    repositories {
        maven { url "https://oss.sonatype.org/content/groups/staging/" }
        maven { url "https://oss.sonatype.org/content/groups/public" }
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        maven { url "https://repo.spring.io/libs-release" }
        maven { url "https://repo.spring.io/snapshot" }
        maven { url "https://dl.bintray.com/rabbitmq/maven-milestones" }
        maven { url "https://plugins.gradle.org/m2/" }
        mavenCentral()
    }
}

subprojects { project ->
    
    group 'com.github.peacetrue'
    version '1.0-SNAPSHOT'
    description '提供开发过程中的各种扩展'
    
    apply plugin: "java"
    apply plugin: "java-library"
    sourceCompatibility = 8
    targetCompatibility = 8

    apply plugin: "idea"
    idea {
        module {
            inheritOutputDirs = false
            outputDir = tasks.compileJava.destinationDir
            testOutputDir = tasks.compileTestJava.destinationDir
            downloadSources = true
            downloadJavadoc = false
        }
    }
    
    apply plugin: 'io.spring.dependency-management'
    dependencyManagement {
        imports {
            mavenBom "org.springframework.boot:spring-boot-dependencies:1.5.20.RELEASE"
        }

        dependencies {
            dependency "com.google.code.findbugs:jsr305:1.3.9"
            dependency 'com.google.guava:guava:24.1-jre'
            dependency 'org.projectlombok:lombok:1.18.6'
            dependency 'org.glassfish:javax.xml.bind:10.0-b28'
            dependency 'javax.xml.bind:jaxb-api:2.3.0'
            dependency "javax.json:javax.json-api:1.1"
            dependency "javax.annotation:javax.annotation-api:1.3.2"
            dependency 'org.reflections:reflections:0.9.9-RC1'
            dependency 'org.jooq:jool:0.9.13'// for Java 9+
            dependency 'org.jooq:jool-java-8:0.9.13'// for Java 8
            dependency 'org.aspectj:aspectjweaver:1.8.9'
            dependency 'mysql:mysql-connector-java:5.1.39'
            dependency 'com.h2database:h2:1.4.196'
            dependency 'org.mybatis:mybatis:3.4.6'
            dependency 'org.mybatis:mybatis-spring:1.3.0'
            dependency 'org.mybatis.spring.boot:mybatis-spring-boot-starter:1.3.2'
            dependency 'org.mybatis.dynamic-sql:mybatis-dynamic-sql:1.1.2'
            dependency 'com.github.pagehelper:pagehelper:5.1.2'
            dependency 'com.github.pagehelper:pagehelper-spring-boot-starter:1.2.3'
            dependency 'tk.mybatis:mapper:3.5.2'
            dependency 'tk.mybatis:mapper-spring-boot-starter:1.2.3'
            dependency "org.hibernate.javax.persistence:hibernate-jpa-2.1-api:1.0.2.Final"
            dependency 'org.hibernate:hibernate-core:5.2.11.Final'
            dependency 'org.hibernate:hibernate-entitymanager:5.2.16.Final'
            dependency "javax.servlet:javax.servlet-api:3.1.0"
            dependency 'org.hibernate:hibernate-validator:5.2.4.Final'
            dependency 'org.apache.commons:commons-lang3:3.1'
            dependency 'io.github.benas:random-beans:3.7.0'
            dependency 'org.unitils:unitils-core:3.4.2'
        }
    }

    dependencies {
        implementation "org.projectlombok:lombok"
        annotationProcessor "org.projectlombok:lombok"

        testCompile "junit:junit"
        testCompile "io.github.benas:random-beans"
        testCompileOnly "org.projectlombok:lombok"
        testAnnotationProcessor "org.projectlombok:lombok"
        testCompile 'org.unitils:unitils-core'
    }

    task sourcesJar(type: Jar) {
        archiveClassifier = 'sources'
        from sourceSets.main.allJava
    }

    task javadocJar(type: Jar) {
        archiveClassifier = 'javadoc'
        from javadoc.destinationDir
    }

    def repository_snapshots = "https://oss.sonatype.org/content/repositories/snapshots/"
    def repository_releases = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"

    apply plugin: 'maven-publish'
    apply plugin: 'signing'
    publishing {
        publications {
            mavenJava(MavenPublication) {
                pom {
                    name = project.name
                    description = project.description
                    url = 'https://github.com/peacetrue'
                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                            id = 'xiayouxue'
                            name = 'xiayouxue'
                            email = 'xiayouxue@hotmail.com'
                        }
                    }
                    scm {
                        connection = 'https://github.com/peacetrue.git'
                        developerConnection = 'https://github.com/peacetrue.git'
                        url = 'https://github.com/peacetrue/peacetrue'
                    }
                }

                from components.java

                artifact sourcesJar
                artifact javadocJar
            }
        }

        repositories {
            maven {
                url version.endsWith('SNAPSHOT') ? repository_snapshots : repository_releases
                credentials {
                    username = ossrhUsername
                    password = ossrhPassword
                }
            }
        }
    }

    signing {
        sign publishing.publications.mavenJava
    }

}







